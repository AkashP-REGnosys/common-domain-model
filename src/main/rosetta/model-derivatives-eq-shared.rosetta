namespace "org.isda.cdm"

class DeterminationMethod <"The purpose of this class is to be extended by the Price and EquityValuation classes.">
{
	determinationMethod DeterminationMethodEnum (0..1) <"Specifies the method according to which an amount or a date is determined.">;
		[synonym FpML_5_10 value determinationMethod]
}

class DividendConditions <"A class describing the conditions governing the payment of dividends to the receiver of the equity return, with the exception of the dividend payout ratio, which is defined for each of the underlying components.">
	[synonym FpML_5_10 value DividendConditions]
{
	dividendReinvestment boolean (0..1) <"Boolean element that defines whether the dividend will be reinvested or not.">;
		[synonym FpML_5_10 value dividendReinvestment]
	dividendEntitlement DividendEntitlementEnum (0..1) <"Defines the date on which the receiver of the equity return is entitled to the dividend.">;
		[synonym FpML_5_10 value dividendEntitlement]
	dividendAmount DividendAmountTypeEnum (0..1) <"Specifies whether the dividend is paid with respect to the Dividend Period.">;
		[synonym FpML_5_10 value dividendAmount]
	dividendPaymentDate DividendPaymentDate (0..1) <"Specifies when the dividend will be paid to the receiver of the equity return. Has the meaning as defined in the ISDA 2002 Equity Derivatives Definitions. Is not applicable in the case of a dividend reinvestment election.">;
		[synonym FpML_5_10 value dividendPaymentDate]
	/*
	 * To be further developed
	 */
}

class DividendDateReference <"A class to specify the dividend date by reference to another date, with the ability to apply and offset. This class doesn't exist in FpML and is meant to simplify the choice constraint associated with the DividendPaymentDate class.">
{
	dateReference DividendDateReferenceEnum (1..1) <"Specification of the dividend date using an enumeration, with values such as the pay date, the ex-date or the record date.">;
		[synonym FpML_5_10 value dateReference]
	paymentDateOffset Offset (0..1) <"Only to be used when SharePayment has been specified in the dividendDateReference element. The number of Currency Business Days following the day on which the Issuer of the Shares pays the relevant dividend to holders of record of the Shares.">;
		[synonym FpML_5_10 value paymentDateOffset]
}

data rule DividendDateReference_paymentDateOffset <"FpML specifies that paymentDateOffset is only to be used when SharePayment has been specified in the dividendDateReference element.">
	when DividendDateReference -> paymentDateOffset exists
	then DividendDateReference -> dateReference = DividendDateReferenceEnum.SharePayment

class DividendPaymentDate one of <"A class describing the date on which the dividend will be paid/received. This class is also used to specify the date on which the FX rate will be determined, when applicable.">
	[synonym FpML_5_10 value DividendPaymentDate]
{
	dividendDateReference DividendDateReference (0..1);
	adjustableDate AdjustableDate (0..1);
		[synonym FpML_5_10 value adjustableDate]
}

class EquityPayoutValuationPrice extends DeterminationMethod
	[synonym FpML_5_10 value ReturnLegValuationPrice]
{
	initialPrice Price (1..1) <"Specifies the initial reference price of the underlier. This price can be expressed either as an actual amount/currency, as a determination method, or by reference to another value specified in the swap document.">;
		[synonym FpML_5_10 value initialPrice path "rateOfReturn"]
	valuationPriceInterim EquityValuation (0..1) <"Specifies the interim valuation price(s) of the underlier. This price can be expressed either as an actual amount/currency, as a determination method, or by reference to another value specified in the swap document.">;
		[synonym FpML_5_10 value valuationPriceInterim path "rateOfReturn"]
	valuationPriceFinal EquityValuation (1..1) <"Specifies the final valuation price of the underlier. This price can be expressed either as an actual amount/currency, as a determination method, or by reference to another value specified in the swap document.">;
		[synonym FpML_5_10 value valuationPriceFinal path "rateOfReturn"]
}

class Price extends DeterminationMethod <"A class describing the strike price."> // TODO - Look for further synergies with the option strike
	[synonym FpML_5_10 value Price]
{
	grossPrice ActualPrice (0..1) <"Specifies the price of the underlier, before commissions.">;
		[synonym FpML_5_10 value grossPrice]
	netPrice ActualPrice (1..1) <"Specifies the price of the underlier, net of commissions.">;
		[synonym FpML_5_10 value netPrice]
	accruedInterest number (0..1) <"Specifies the accrued interest that are part of the dirty price in the case of a fixed income security or a convertible bond. Expressed in percentage of the notional.">;
		[synonym FpML_5_10 value accruedInterestPrice]
	/*
	 * FpML construct also includes other elements, such as the fxConversion
	 */
}

class Return <"A type describing the dividend return conditions applicable to the equity payout.">
	[synonym FpML_5_10 value Return]
{
	returnType ReturnTypeEnum (1..1) <"Specifies the type of return associated with the equity payout.">;
		[synonym FpML_5_10 value returnType]
	dividendConditions DividendConditions (0..1) <"Specifies the conditions governing the payment of the dividends to the receiver of the equity return, with the exception of the dividend payout ratio, which is defined for each of the underlying components.">;
		[synonym FpML_5_10 value dividendConditions]
}

/*
 * Tentative WIP to reflect the 2011 terminology + work around the FpML LegAmount and ReturnSwapAmount components, which seem quite complex
 * Also, the concept of referenceAmount seems central to the FpML build, but relies upon a referenceAmountScheme which is not specified by FpML
 */
class SettlementFeatures
{
	cashSettlement boolean (0..1);
		[synonym FpML_5_10 value cashSettlement]
	settlementCurrency string (0..1) scheme;
		[synonym FpML_5_10 value currency meta currencyScheme]
}

class EquityValuation <"A class for defining how and when an equity option  or equity swap is to be valued.">
	[synonym FpML_5_10 value EquityValuation]
{
	determinationMethod DeterminationMethodEnum (0..1) <"Specifies the method according to which an amount or a date is determined.">;
		[synonym FpML_5_10 value determinationMethod]
	valuationDates AdjustableOrRelativeDates (0..1) <"The term 'Valuation Date' is assumed to have the meaning as defined in the ISDA 2002 Equity Derivatives Definitions. Cash Settlement Payment Date in accordance with the ISDA 2002 Equity Derivatives Definitions. SettlementCycle in accordance with the ISDA 2011 Equity Derivatives Definitions.">;
		[synonym FpML_5_10 value valuationDates path "valuationRules"]
	valuationDate AdjustableOrRelativeDate (0..1) <"The term 'Valuation Date' is assumed to have the meaning as defined in the ISDA 2002 Equity Derivatives Definitions. Cash Settlement Payment Date in accordance with the ISDA 2002 Equity Derivatives Definitions. SettlementCycle in accordance with the ISDA 2011 Equity Derivatives Definitions.">;
		[synonym FpML_5_10 value valuationDate path "valuationRules"]
	/*
	 * The FpML construct has a a number of other attributes: valuation time, dividend valuation dates, fall-back provision, ...
	 * Also, FpML extends Price while the current CDM approach is just to share the determinationMethod.  To be confirmed as a function of the use cases.
	 */
}