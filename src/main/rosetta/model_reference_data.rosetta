namespace "org.isda.cdm"

class Account <"The account specification.">
	[synonym FpML value Account]
{
	id string (0..1) anchor;
		[synonym FpML value id]
	accountNumber string (1..1) scheme "accountIdScheme" <"The account number.">;
		[synonym FpML value accountId]
	accountName string (0..1) scheme "accountNameScheme" <"The name by which the account is known.">;
		[synonym FpML value accountName]
	accountType AccountTypeEnum (0..1) scheme "accountTypeScheme" <"The type of account, e.g. client, house.">;
		[synonym FpML value accountType]
	servicingParty string (0..1) reference <"The reference to the legal entity that services the account, i.e. in the books of which the account is held.">;
		[synonym FpML value servicingParty]
}

class LegalEntity <"A class to represent the attributes that are specific to a legal entity.">
{
	id string (0..1);
		[synonym FpML value id]
	entityId string (0..1) scheme "entityIdScheme" <"A legal entity identifier (e.g. RED entity code).">;
		[synonym FpML value entityId]
	name string (1..1) scheme "entityNameScheme" <"The legal entity name.">;
		[synonym FpML value partyName]
		[synonym FpML value entityName]
}

class NaturalPerson <"A class to represent the attributes that are specific to a natural person.">
{
	honorific string (0..1) <"An honorific title, such as Mr., Ms., Dr. etc.">;
		[synonym FpML value honorific]
	firstName string (1..1) <"The natural person's first name. It is optional in FpML.">;
		[synonym FpML value firstName]
	middleName string (0..*);
		[synonym FpML value firstName]
	initial string (0..*);
		[synonym FpML value initial]
	surname string (1..1) <"The natural person's surname. It is optional in FpML.">;
		[synonym FpML value surname]
	suffix string (0..1) <"Name suffix, such as Jr., III, etc.">;
		[synonym FpML value suffix]
	dateOfBirth date (1..1) <"The natural person's date of birth.">;
		[synonym FpML value dateOfBirth]
}

choice rule NaturalPerson_choice <"Choice rule to represent an FpML choice construct.">
	for NaturalPerson optional choice between
	middleName and initial

class Party <"The party class.">
	[synonym FpML value Party]
{
	id string (0..1) anchor;
		[synonym FpML value id]
	partyId string (1..1) scheme "partyIdScheme" <"The identifier associated with a party, e.g. the 20 digits LEI code.">;
		[synonym FpML value partyId]
		[synonym Rosetta_Workbench value partyId]
	legalEntity LegalEntity (0..1);
	naturalPerson NaturalPerson (0..1);
}

choice rule Party_choice <"A party is either a legal entity or a natural person.">
	for Party optional choice between
	legalEntity and naturalPerson

class PartyAndAccountReference <"This class corresponds to the FpML PartyAndAccountReferences.model.">
{
	partyReference string (1..1) reference;
		[synonym FpML value partyReference]
	accountReference string (0..1) reference;
		[synonym FpML value accountReference]
}

class PartyRole
{
	partyReference string (1..1) reference <"A reference to the party to which the role refers to.">;
		[synonym FpML value buyerPartyReference]
	role PartyRoleEnum (0..1) <"The party role.">;
		[synonym FpML value role]
	businessCenter BusinessCenterEnum (0..1) <"The business center in which the party which is referenced as part of the role is located. This information is specified for the ISDA Calculation Agent, as the short-form confirm for a contract that is executed under a Sovereign or Asia-Pacific Master Confirmation Agreement (MCA) does need to specify the Calculation Agent city. This is due to the fact that the MCA sets the value for Calculation Agent but does not set the value for Calculation Agent city.">;
}

class NaturalPersonRole
{
	personReference string (1..1) reference <"A reference to the natural person to whom the role refers to.">;
		[synonym FpML value buyerPartyReference]
	role NaturalPersonRoleEnum (0..1) <"FpML specifies a person role that is distinct from the party role.">;
		[synonym FpML value role] 
}