Rosetta versionNumber "0.0.1"
FIX versionNumber "5.0 SP3"
FpML versionNumber "5.10"

class ReportingEventHeader stereotype regulatoryReporting <"The regulatory reporting information is that is common among regulators.">
{
	reportingRegime ReportingRegime (1..1) <"The reporting regime to which the transaction is reported.">;
	reportingTime time (1..1) <"The date and time on which the transaction is actually reported">;
	reportingParty Party (1..1) <"The party submitting the regulatory report to the competent authority.">;
		[synonym ISO_20022 value SubmitgPty]
		[regulatoryReference ESMA_MiFIR specification "ISO 20022 - Part 2" section "20.1.11.2.4" provision "SubmittingParty <SubmitgPty> - Definition: Entity submitting the transaction report to the competent authority."]
}

/* 
 * Clarify why in FpML the reporting regime name is optional as a result of the choice construct
 * There are also more attributes as part of the FpML Reporting, that need to be better evaluated for the purpose
 * of the canonical model
 */
class ReportingRegime stereotype regulatoryEligibility, regulatoryReporting <"The regulatory reporting regime, which is composed of a reporting regime name and a regulator.">
	[synonym FpML value ReportingRegime]
{
	reportingRegime ReportingRegimeEnum (1..1) <"The reporting regime name.">;
		[synonym FpML value name]
	regulator Regulator (1..*) <"The regulator to which the reporting will take place.  A transaction eligible under one regime could be reportable to multiple regulators (e.g. MiFID II can be reported to multiple national regulators.">;
}

class Regulator stereotype regulatoryEligibility, regulatoryReporting <"The regulator to which a transaction needs to be reported to, alongside the registration number of the reporting party, when relevant.">
{
	supervisoryBody SupervisoryBodyEnum (1..1) <"The regulator or other supervisory body to which the transaction will be reported.">;
		[synonym FpML value supervisoryBody]
	registrationNumber string (0..1) <"The registration number identifier assigned by the regulator.">;
		[synonym FpML value registrationNumber]
}

