namespace org.isda.cdm
version "${project.version}"

import cdm.base.*
import cdm.base.datetime.*
import cdm.observable.asset.*
import cdm.observable.asset.*
import cdm.synonyms.config.*

type ForwardPayout: <"Representation of a forward settling payout. The 'Underlier' attribute captures the underlying payout, which is settled according to the 'SettlementTerms' attribute. Both FX Spot and FX Forward should use this componen, while distinction between the two will be handled via product qualification.">

	underlier Underlier (1..1) <"Underlying product that the forward is written on, which can be of any type: FX, a contractual product, a security, etc.">
	settlementTerms OptionSettlement (1..1) <"Settlement terms for the underlier that include the settlement date, settlement method etc.">
	condition SettlementDate: <"For foreign exchange contracts, either the settlementDate is set or the cashflowDates, but not both. When the cashflowDates are set, they must be the same for the 2 legs of the currency pair.">
		if ForwardFX() exists
		then ( settlementTerms -> valueDate exists
			and ForwardFX() -> exchangedCurrency1 -> cashflowDate is absent and ForwardFX() -> exchangedCurrency2 -> cashflowDate is absent)
			or ( settlementTerms -> valueDate is absent
			and ForwardFX() -> exchangedCurrency1 -> cashflowDate exists and ForwardFX() -> exchangedCurrency2 -> cashflowDate exists and
			ForwardFX() -> exchangedCurrency1 -> cashflowDate = ForwardFX() -> exchangedCurrency2 -> cashflowDate) 
	condition FxSettlement: <"For foreign exchange contracts, the settlement type must be either fx non-deliverable settlement or not specified, which implies physical settlement in the case of foreign exchange.">
		if ForwardFX() exists 
		then ( settlementTerms -> cashSettlementTerms is absent
			and settlementTerms -> physicalSettlementTerms is absent)

// NOW SUPERSEDED By THE USE OF GENERIC 'PRODUCT' UNDERLIER, WHICH ALREADY HAS A 'ONE OF' RULE:
// choice rule ForwardPayout_choice <"The forward payout must have an underlying asset that is either a security or a foreign exchange contract.">
// 	for ForwardPayout required choice between 
// 	settlementTerms and underlier
type ForeignExchange: <"From FpML: A type defining either a spot or forward FX transactions.">
	[synonym FpML_5_10 value "FxSingleLeg"]
	exchangedCurrency1 Cashflow (1..1) <"This is the first of the two currency flows that define a single leg of a standard foreign exchange transaction.">
		[synonym FpML_5_10 value "exchangedCurrency1"]
	exchangedCurrency2 Cashflow (1..1) <"This is the second of the two currency flows that define a single leg of a standard foreign exchange transaction.">
		[synonym FpML_5_10 value "exchangedCurrency2"]
	tenorPeriod Period (0..1) <"A tenor expressed as a period type and multiplier (e.g. 1D, 1Y, etc.)">
		[synonym FpML_5_10 value "tenorPeriod"]
	exchangeRate ExchangeRate (0..1) <"ATTRIBUTE SCHEDULED FOR DEPRECATION. The rate of exchange between the two currencies.">
		[synonym FpML_5_10 value "ignore"]
		

type FxCashSettlement: <"A class that is used for describing cash settlement of an option / non deliverable forward. It includes the currency to settle into together with the fixings required to calculate the currency amount.">
	[synonym FpML_5_10 value "FxCashSettlement"]

	settlementCurrency string (1..1) <"The currency in which cash settlement occurs for non-deliverable forwards and cash-settled options (non-deliverable or otherwise).">
		[metadata scheme]
		[synonym FpML_5_10 value "settlementCurrency"]
	referenceCurrency string (0..1)
		[metadata scheme]
		[synonym FpML_5_10 value "referenceCurrency"]
	fixing FxFixing (0..*) <"Specifies the source for and timing of a fixing of an exchange rate. This is used in the agreement of non-deliverable forward trades as well as various types of FX OTC options that require observations against a particular rate. This element is optional, permitting it to be omitted where fixing details are unavailable at the point of message creation. It has multiple occurrence to support the case where fixing details must be specified for more than one currency pair e.g. on an option settled into a third currency (that is not one of the option currencies).">
		[synonym FpML_5_10 value "fixing"]
	rateSourceFixing FxRateSourceFixing (0..*) <"Specifies the source for and timing of a fixing of an exchange rate. This is used in the agreement of non-deliverable forward trades as well as various types of FX OTC options that require observations against a particular rate. This element is optional, permitting it to be omitted where fixing details are unavailable at the point of message creation. It has multiple occurrence to support the case where fixing details must be specified for more than one currency pair e.g. on an option settled into a third currency (that is not one of the option currencies).">
		[synonym FpML_5_10 value "rateSourceFixing"]

	condition FixingChoice:
		optional choice fixing, rateSourceFixing

type FxFixing: <"A class that specifies the source for and timing of a fixing of an exchange rate. This is used in the agreement of non-deliverable forward trades as well as various types of FX OTC options that require observations against a particular rate.">
	[synonym FpML_5_10 value "FxFixing"]

	quotedCurrencyPair QuotedCurrencyPair (1..1) <"Defines the two currencies for an FX trade and the quotation relationship between the two currencies.">
		[synonym FpML_5_10 value "quotedCurrencyPair"]
	fixingDate date (0..1) <"Describes the specific date when a non-deliverable forward or cash-settled option will 'fix' against a particular rate, which will be used to compute the ultimate cash settlement. This element should be omitted where a single, discrete fixing date cannot be identified e.g. on an american option, where fixing may occur at any date on a continuous range.">
		[synonym FpML_5_10 value "fixingDate"]
	fxSpotRateSource FxSpotRateSource (0..1) <"Specifies the methodology (reference source and, optionally, fixing time) to be used for determining a currency conversion rate.">
		[synonym FpML_5_10 value "fxSpotRateSource"]

type FxRateSourceFixing: <"Describes a rate source to be fixed and the date the fixing occurs">
	[synonym FpML_5_10 value "FxRateSourceFixing"]

	settlementRateSource FxSettlementRateSource (1..1)
		[synonym FpML_5_10 value "settlementRateSource"]
	fixingDate AdjustableDate (1..1) <"The date on which the fixing is scheduled to occur.">
		[synonym FpML_5_10 value "fixingDate"]


type FxSettlementRateSource: <"The source of the Foreign Exchange settlement rate.">
	[synonym FpML_5_10 value "FxSettlementRateSource"]

	settlementRateOption string (0..1) <"Indicates that an officially defined rate settlement rate option will be the used for the fixing.">
		[metadata scheme]
		[synonym FpML_5_10 value "settlementRateOption"]
	nonstandardSettlementRate FxInformationSource (0..1) <"Indicates that a non-standard rate source will be used for the fixing.">

	condition Choice:
		required choice settlementRateOption, nonstandardSettlementRate

type FxInformationSource extends InformationSource: <"Information source specific to Foreign Exchange products.">
	[synonym FpML_5_10 value "FxInformationSource"]

	fixingTime BusinessCenterTime (0..1) <"The time that the fixing will be taken along with a business center to define the time zone.">

