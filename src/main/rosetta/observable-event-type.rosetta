namespace cdm.observable.event : <"Observable event concepts: extraordinary event, trigger event, disruption event etc.">
version "${project.version}"

import cdm.base.*
import cdm.base.datetime.*
import cdm.base.staticdata.party.*
import cdm.observable.asset.*
import cdm.observable.event.*

type AdditionalDisruptionEvents: <"A type for defining the Additional Disruption Events.">
	[synonym FpML_5_10 value "AdditionalDisruptionEvents"]

	changeInLaw boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Change in Law | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Change In Law | If true, then change in law is applicable.">
		[synonym FpML_5_10 value "changeInLaw"]
	failureToDeliver boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Failure to Deliver | Where the underlying is shares and the transaction is physically settled, then, if true, a failure to deliver the shares on the settlement date will not be an event of default for the purposes of the master agreement.">
		[synonym FpML_5_10 value "failureToDeliver"]
	insolvencyFiling boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Insolvency Filing | If true, then insolvency filing is applicable.">
		[synonym FpML_5_10 value "insolvencyFiling"]
	hedgingDisruption boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Hedging Disruption | If true, then hedging disruption is applicable.">
		[synonym FpML_5_10 value "hedgingDisruption"]
	increasedCostOfHedging boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Increased Cost of Hedging | If true, then increased cost of hedging is applicable.">
		[synonym FpML_5_10 value "increasedCostOfHedging"]
	foreignOwnershipEvent boolean (0..1) <"If true, then foreign ownership event is applicable.">
		[synonym FpML_5_10 value "foreignOwnershipEvent"]
	lossOfStockBorrow boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Loss of Stock Borrow | If true, then loss of stock borrow is applicable.">
		[synonym FpML_5_10 value "lossOfStockBorrow"]
	maximumStockLoanRate number (0..1) <"Specifies the maximum stock loan rate for Loss of Stock Borrow. A percentage of 5% is represented as 0.05.">
		[synonym FpML_5_10 value "maximumStockLoanRate"]
	increasedCostOfStockBorrow boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Increased Cost of Stock Borrow | If true, then increased cost of stock borrow is applicable.">
		[synonym FpML_5_10 value "increasedCostOfStockBorrow"]
	initialStockLoanRate number (0..1) <"Specifies the initial stock loan rate for Increased Cost of Stock Borrow. A percentage of 5% is represented as 0.05.">
		[synonym FpML_5_10 value "initialStockLoanRate"]
	determiningPartyReference Party (0..1) <"A reference to the party which determines additional disruption events.">
		[metadata reference]
		[synonym FpML_5_10 value "determiningPartyReference" meta "href"]

	condition MaximumStockLoanRate: <" FpML specifies the maximumStockLoanRate as a RestrictedPercentage, meaning that its value is comprised between 0 and 1.">
		if maximumStockLoanRate exists
		then maximumStockLoanRate >= 0
			and maximumStockLoanRate <= 1

	condition InitialStockLoanRate: <" FpML specifies the initialStockLoanRate as a RestrictedPercentage, meaning that its value is comprised between 0 and 1.">
		if initialStockLoanRate exists
		then AdditionalDisruptionEvents ->initialStockLoanRate >= 0
			and initialStockLoanRate <= 1


/*
 * The FpML construct has a a number of other attributes: dividend valuation dates, fall-back provision, ...
 * Also, FpML extends Price while the current CDM approach is just to share the determinationMethod.  To be confirmed as a function of the use cases.
 */
type ExtraordinaryEvents: <"Where the underlying is shares, defines market events affecting the issuer of those shares that may require the terms of the transaction to be adjusted.">
	[synonym FpML_5_10 value "ExtraordinaryEvents"]

	mergerEvents EquityCorporateEvents (0..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Merger Event shall occur if a Merger occurs and the Merger Date is on or before the final Equity Valuation Date | Occurs when the underlying ceases to exist following a merger between the Issuer and another company.">
		[synonym FpML_5_10 value "mergerEvents"]
	tenderOfferEvents EquityCorporateEvents (0..1) <"2002 ISDA Equity Derivatives Definitions: Tender Offers | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Tender Offer Event">
		[synonym FpML_5_10 value "tenderOfferEvents"]
	compositionOfCombinedConsideration boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Composition of Combined Consideration | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Combined Consideration | If present and true, then composition of combined consideration is applicable.">
		[synonym FpML_5_10 value "compositionOfCombinedConsideration"]
	indexAdjustmentEvents IndexAdjustmentEvents (0..1) <"2002 ISDA Equity Derivatives Definitions: Adjustments to Indices | ">
		[synonym FpML_5_10 value "indexAdjustmentEvents"]
	additionalDisruptionEvents AdditionalDisruptionEvents (0..1) <"2002 ISDA Equity Derivatives Definitions: Additional Disruption Events | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Additional Disruption Events means each Additional Disruption Event specified in the Confirmation Side Letter. For the avoidance of doubt, each Additional Disruption Event shall be an Extraordinary Event.">
		[synonym FpML_5_10 value "additionalDisruptionEvents"]
	failureToDeliver boolean (0..1) <"If true, failure to deliver is applicable.">
		[synonym FpML_5_10 value "failureToDeliver"]
	representations Representations (0..1)
		[synonym FpML_5_10 value "representations"]
	nationalizationOrInsolvency NationalizationOrInsolvencyOrDelistingEventEnum (0..1) <"2002 ISDA Equity Derivatives Definitions: Nationalization and Insolvency | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Nationalization shall occur if all the Securities or all or substantially all the assets of an Issuer are nationalized, expropriated or are otherwise required to be transferred to any governmental agency, authority, entity or instrumentality thereof. Insolvency Filing means as defined in the Confirmation Side Letter.">
		[synonym FpML_5_10 value "nationalisationOrInsolvency"]
	delisting NationalizationOrInsolvencyOrDelistingEventEnum (0..1) <"2002 ISDA Equity Derivatives Definitions: Delisting | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Delisting means Delisting (Broad Relisting) or Delisting (Narrow Relisting), as specified in the Relationship Supplement.">
		[synonym FpML_5_10 value "delisting"]
	condition Choice: <"condition to represent an FpML choice construct.">
		required choice additionalDisruptionEvents, failureToDeliver

type IndexAdjustmentEvents: <"Defines the specification of the consequences of Index Events as defined by the 2002 ISDA Equity Derivatives Definitions.">
	[synonym FpML_5_10 value "IndexAdjustmentEvents"]

	indexModification IndexEventConsequenceEnum (1..1) <"Consequence of index modification.">
		[synonym FpML_5_10 value "indexModification"]
	indexCancellation IndexEventConsequenceEnum (1..1) <"Consequence of index cancellation.">
		[synonym FpML_5_10 value "indexCancellation"]
	indexDisruption IndexEventConsequenceEnum (1..1) <"Consequence of index disruption.">
		[synonym FpML_5_10 value "indexDisruption"]

type Trigger: <"Trigger point at which feature is effective.">
	[synonym FpML_5_10 value "Trigger"]

	level number (0..1) <"The trigger level.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "level"]
	levelPercentage number (0..1) <"The trigger level percentage.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "levelPercentage"]
	creditEvents CreditEvents (0..1)
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "creditEvents"]
	creditEventsReference CreditEvents (0..1)
		[metadata reference]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "creditEventsReference" meta "href"]
	triggerType TriggerTypeEnum (0..1) <"The Triggering condition.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "triggerType"]
	triggerTimeType TriggerTimeTypeEnum (0..1) <"The valuation time type of knock condition.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "triggerTimeType"]

	condition Choice1: <" Choice rule to represent an FpML choice construct.">
		required choice level, levelPercentage , creditEvents , creditEventsReference

	// condition Choice2: <" Choice rule to represent an FpML choice construct.">
	// 	required choice creditEvents, creditEventsReference

type TriggerEvent: <"Observation point for trigger.">
	[synonym FpML_5_10 value "TriggerEvent"]

	schedule AveragingSchedule (0..*) <"A derivative schedule.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "schedule"]
	triggerDates DateList (0..1) <"The trigger Dates.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "triggerDates"]
	trigger Trigger (1..1) <"The trigger level">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "trigger"]
	featurePayment FeaturePayment (0..1) <"The feature payment, i.e. the payment made following trigger occurrence.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "featurePayment"]

type EquityCorporateEvents: <"A class for defining the merger events and their treatment.">
	[synonym FpML_5_10 value "EquityCorporateEvents"]

	shareForShare ShareExtraordinaryEventEnum (1..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Merger Event (S-F-S) shall occur if a Merger Event occurs and the consideration for the relevant Security consists solely of Combined Consideration. | The consideration paid for the original shares following the Merger Event consists wholly of new shares.">
		[synonym FpML_5_10 value "shareForShare"]
	shareForOther ShareExtraordinaryEventEnum (1..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Merger Event (S-F-O) shall occur if a Merger Event occurs and the consideration for the relevant Security consists solely of Other Consideration. | The consideration paid for the original shares following the Merger Event consists wholly of cash/securities other than new shares.">
		[synonym FpML_5_10 value "shareForOther"]
	shareForCombined ShareExtraordinaryEventEnum (1..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Merger Event (S-F-C) shall occur if a Merger Event occurs and the consideration for the relevant Security consists solely of Combined Consideration. | The consideration paid for the original shares following the Merger Event consists of both cash/securities and new shares.">
		[synonym FpML_5_10 value "shareForCombined"]

// In a subsequent phase of work, a single price should be stored and "reset" after each period. Lineage should then be used to resolve the period start and end prices for the Equity Cash Settlement Amount calculation
type Representations:
	[synonym FpML_5_10 value "Representations"]

	nonReliance boolean (1..1) <"If true, then non reliance is applicable.">
		[synonym FpML_5_10 value "nonReliance"]
	agreementsRegardingHedging boolean (1..1) <"If true, then agreements regarding hedging are applicable.">
		[synonym FpML_5_10 value "agreementsRegardingHedging"]
	indexDisclaimer boolean (1..1) <"If present and true, then index disclaimer is applicable.">
		[synonym FpML_5_10 value "indexDisclaimer"]
	additionalAcknowledgements boolean (0..1) <"If true, then additional acknowledgements are applicable.">
		[synonym FpML_5_10 value "additionalAcknowledgements"]

// asset tag types
type CreditEvents: <"A class to specify the applicable Credit Events that would trigger a settlement, as specified in the related Confirmation and defined in the ISDA 2014 Credit Definition article IV section 4.1.">
	[metadata key]
	[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "CreditEvents" meta "id"]

	bankruptcy boolean (0..1) <"A credit event. The reference entity has been dissolved or has become insolvent. It also covers events that may be a precursor to insolvency such as instigation of bankruptcy or insolvency proceedings. Sovereign trades are not subject to Bankruptcy as 'technically' a Sovereign cannot become bankrupt. ISDA 2003 Term: Bankruptcy.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "bankruptcy"]
	failureToPay FailureToPay (0..1) <"A credit event. This credit event triggers, after the expiration of any applicable grace period, if the reference entity fails to make due payments in an aggregate amount of not less than the payment requirement on one or more obligations (e.g. a missed coupon payment). ISDA 2003 Term: Failure to Pay.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "failureToPay"]
	failureToPayPrincipal boolean (0..1) <"A credit event. Corresponds to the failure by the Reference Entity to pay an expected principal amount or the payment of an actual principal amount that is less than the expected principal amount. ISDA 2003 Term: Failure to Pay Principal.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "failureToPayPrincipal"]
	failureToPayInterest boolean (0..1) <"A credit event. Corresponds to the failure by the Reference Entity to pay an expected interest amount or the payment of an actual interest amount that is less than the expected interest amount. ISDA 2003 Term: Failure to Pay Interest.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "failureToPayInterest"]
	obligationDefault boolean (0..1) <"A credit event. One or more of the obligations have become capable of being declared due and payable before they would otherwise have been due and payable as a result of, or on the basis of, the occurrence of a default, event of default or other similar condition or event other than failure to pay. ISDA 2003 Term: Obligation Default.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "obligationDefault"]
	obligationAcceleration boolean (0..1) <"A credit event. One or more of the obligations have been declared due and payable before they would otherwise have been due and payable as a result of, or on the basis of, the occurrence of a default, event of default or other similar condition or event other than failure to pay (preferred by the market over Obligation Default, because more definitive and encompasses the definition of Obligation Default - this is more favorable to the Seller). Subject to the default requirement amount. ISDA 2003 Term: Obligation Acceleration.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "obligationAcceleration"]
	repudiationMoratorium boolean (0..1) <"A credit event. The reference entity, or a governmental authority, either refuses to recognise or challenges the validity of one or more obligations of the reference entity, or imposes a moratorium thereby postponing payments on one or more of the obligations of the reference entity. Subject to the default requirement amount. ISDA 2003 Term: Repudiation/Moratorium.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "repudiationMoratorium"]
	restructuring Restructuring (0..1) <"A credit event. A restructuring is an event that materially impacts the reference entity's obligations, such as an interest rate reduction, principal reduction, deferral of interest or principal, change in priority ranking, or change in currency or composition of payment. ISDA 2003 Term: Restructuring.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "restructuring"]
	governmentalIntervention boolean (0..1) <"A credit event. A governmental intervention is an event resulting from an action by a governmental authority that materially impacts the reference entity's obligations, such as an interest rate reduction, principal reduction, deferral of interest or principal, change in priority ranking, or change in currency or composition of payment. ISDA 2014 Term: Governmental Intervention.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "governmentalIntervention"]
	distressedRatingsDowngrade boolean (0..1) <"A credit event. Results from the fact that the rating of the reference obligation is down-graded to a distressed rating level. From a usage standpoint, this credit event is typically not applicable in case of RMBS trades.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "distressedRatingsDowngrade"]
	maturityExtension boolean (0..1) <"A credit event. Results from the fact that the underlier fails to make principal payments as expected.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "maturityExtension"]
	writedown boolean (0..1) <"A credit event. Results from the fact that the underlier writes down its outstanding principal amount.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "writedown"]
	impliedWritedown boolean (0..1) <"A credit event. Results from the fact that losses occur to the underlying instruments that do not result in reductions of the outstanding principal of the reference obligation.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "impliedWritedown"]
	defaultRequirement Money (0..1) <"In relation to certain credit events, serves as a threshold for Obligation Acceleration, Obligation Default, Repudiation/Moratorium and Restructuring. Market standard is USD 10,000,000 (JPY 1,000,000,000 for all Japanese Yen trades). This is applied on an aggregate or total basis across all Obligations of the Reference Entity. Used to prevent technical/operational errors from triggering credit events. ISDA 2003 Term: Default Requirement.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "defaultRequirement"]
	creditEventNotice CreditEventNotice (0..1) <"A specified condition to settlement. An irrevocable written or verbal notice that describes a credit event that has occurred. The notice is sent from the notifying party (either the buyer or the seller) to the counterparty. It provides information relevant to determining that a credit event has occurred. This is typically accompanied by Publicly Available Information. ISDA 2003 Term: Credit Event Notice.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "creditEventNotice"]


type CreditEventNotice:
	[synonym FpML_5_10 value "CreditEventNotice"]

	notifyingParty NotifyingParty (1..1) <"The notifying party is the party that notifies the other party when a credit event has occurred by means of a credit event notice. If more than one party is referenced as being the notifying party then either party may notify the other of a credit event occurring. ISDA 2003 Term: Notifying Party.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "notifyingParty"]
	businessCenter BusinessCenterEnum (0..1) <"Inclusion of this business center element implies that Greenwich Mean Time in Section 3.3 of the 2003 ISDA Credit Derivatives Definitions is replaced by the local time of the city indicated by the businessCenter element value.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "businessCenter"]
	publiclyAvailableInformation PubliclyAvailableInformation (0..1) <"A specified condition to settlement. Publicly available information means information that reasonably confirms any of the facts relevant to determining that a credit event or potential repudiation/moratorium, as applicable, has occurred. The ISDA defined list (2003) is the market standard and is considered comprehensive, and a minimum of two differing public sources must have published the relevant information, to declare a Credit Event. ISDA 2003 Term: Notice of Publicly Available Information Applicable.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "publiclyAvailableInformation"]

type FailureToPay:
	[synonym FpML_5_10 value "notifyingParty"]

	applicable boolean (1..1) <"Indicates whether the failure to pay provision is applicable.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "applicable"]
	gracePeriodExtension GracePeriodExtension (0..1) <"If this element is specified, indicates whether or not a grace period extension is applicable. ISDA 2003 Term: Grace Period Extension Applicable.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "gracePeriodExtension"]
	paymentRequirement Money (0..1) <"Specifies a threshold for the failure to pay credit event. Market standard is USD 1,000,000 (JPY 100,000,000 for Japanese Yen trades) or its equivalent in the relevant obligation currency. This is applied on an aggregate basis across all Obligations of the Reference Entity. Intended to prevent technical/operational errors from triggering credit events. ISDA 2003 Term: Payment Requirement">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "paymentRequirement"]

type GracePeriodExtension:
	[synonym FpML_5_10 value "GracePeriodExtension"]

	applicable boolean (1..1) <"Indicates whether the grace period extension provision is applicable.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "applicable"]
	gracePeriod Offset (0..1) <"The number of calendar or business days after any due date that the reference entity has to fulfil its obligations before a failure to pay credit event is deemed to have occurred. ISDA 2003 Term: Grace Period.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "gracePeriod"]


type NotifyingParty:
	[synonym FpML_5_10 value "NotifyingParty"]

	buyer CounterpartyEnum (0..1)
		[synonym FpML_5_10, CME_ClearedConfirm_1_17 value "buyerPartyReference" mapper "CounterpartyEnum"]
		[synonym CME_SubmissionIRS_1_0 value "buyerPartyReference" mapper "CmeCounterpartyEnum"]
	seller CounterpartyEnum (0..1)
		[synonym FpML_5_10, CME_ClearedConfirm_1_17 value "sellerPartyReference" mapper "CounterpartyEnum"]
		[synonym CME_SubmissionIRS_1_0 value "sellerPartyReference" mapper "CmeCounterpartyEnum"]

type PubliclyAvailableInformation:
	[synonym FpML_5_10 value "PubliclyAvailableInformation"]

	standardPublicSources boolean (0..1) <"If this element is specified and set to 'true', indicates that ISDA defined Standard Public Sources are applicable.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "standardPublicSources"]
	publicSource string (0..*) <"A public information source, e.g. a particular newspaper or electronic news service, that may publish relevant information used in the determination of whether or not a credit event has occurred. ISDA 2003 Term: Public Source.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "publicSource"]
	specifiedNumber int (0..1) <"The minimum number of the specified public information sources that must publish information that reasonably confirms that a credit event has occurred. The market convention is two. ISDA 2003 Term: Specified Number.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "specifiedNumber"]

	condition SourceChoice: <" FpML validation rule cd-36 - Context: PubliclyAvailableInformation (complex type). Either standardPublicSources or at least one publicSource element must exist.">
		required choice standardPublicSources, publicSource

	condition PositiveSpecifiedNumber: <" FpML specifies specifiedNumber as a positiveInteger.">
		if specifiedNumber exists
		then specifiedNumber >= 0

type Restructuring:
	[synonym FpML_5_10 value "Restructuring"]

	applicable boolean (1..1) <"Indicates whether the restructuring provision is applicable.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "applicable"]
	restructuringType RestructuringEnum (0..1) <"Specifies the type of restructuring that is applicable.">
		[metadata scheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "restructuringType" meta "restructuringScheme"]
	multipleHolderObligation boolean (0..1) <"In relation to a restructuring credit event, unless multiple holder obligation is not specified restructurings are limited to multiple holder obligations. A multiple holder obligation means an obligation that is held by more than three holders that are not affiliates of each other and where at least two thirds of the holders must agree to the event that constitutes the restructuring credit event. ISDA 2003 Term: Multiple Holder Obligation.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "multipleHolderObligation"]
	multipleCreditEventNotices boolean (0..1) <"Presence of this element and value set to 'true' indicates that Section 3.9 of the 2003 Credit Derivatives Definitions shall apply. Absence of this element indicates that Section 3.9 shall not apply. NOTE: Not allowed under ISDA Credit 1999.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "multipleCreditEventNotices"]




